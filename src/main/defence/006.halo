# Reverse Polish Notation

fun main():
    var stack = Stack();
    var token = readln();

    while token:
        if token == "+":
            var right = stack.pop();
            var left = stack.pop();
            stack.push(left + right);
        elif token == "-":
            var right = stack.pop();
            var left = stack.pop();
            stack.push(left - right);
        elif token == "*":
            var right = stack.pop();
            var left = stack.pop();
            stack.push(left * right);
        elif token == "/":
            var right = stack.pop();
            var left = stack.pop();
            stack.push(left / right);
        else:
            stack.push(to_int(token));    
        end

        println(stack);
        let token = readln();
    end

    if stack.size() == 1:
        println("res: " + to_str(stack.top()));
    else:
        println("incorrect postfix expression");
    end
end

class Stack:
    var top_node;
    var curr_len;

    fun _init_():
        let my.curr_len = 0;
    end
    
    fun push(data):
        let my.top_node = Node(data, my.top_node);
        let my.curr_len = my.curr_len + 1;
    end

    fun pop():
        if not my.top_node:
            error("attempt to pop from an empty stack");
        end
        var res = my.top_node.data;
        let my.top_node = my.top_node.next;
        let my.curr_len = my.curr_len - 1;
        return res;
    end

    fun top():
        if not my.top_node:
            error("attempt to access element in an empty stack");
        end
        return my.top_node.data;
    end

    fun size():
        return my.curr_len;
    end
end

class Node:
    var data;
    var next;

    fun _init_(data, next):
        let my.data = data;
        let my.next = next;
    end
end

main();